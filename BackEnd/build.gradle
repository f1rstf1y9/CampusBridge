plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'

	// (Spring rest Doc)1. asciidoctor 플러그인 추가
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.tnasfer'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	//(Spring rest Doc) 2. configuration 추가
	asciidoctorExt
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
//	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	//mapper
	implementation 'org.mapstruct:mapstruct:1.5.0.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.0.Final'

	//JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

	//json Writer
	implementation 'com.google.code.gson:gson'

	//aws s3
	implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.1.1")
	implementation("io.awspring.cloud:spring-cloud-aws-starter-s3:3.1.1")

	// 3. (Spring rest Doc) REST Docs Implementation
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	//GOOGLE OCR
	implementation 'com.google.cloud:google-cloud-vision:3.34.0'

	//PDF 이미지 변환
	implementation 'org.apache.pdfbox:pdfbox:2.0.29'

}
// (Spring rest Doc) 4. sinppetsDir 추가
ext {
	snippetsDir = file('build/generated-snippets')
}


test {
	outputs.dir snippetsDir
	useJUnitPlatform() // 이걸 써야 snippet들이 만들어지는 게 허가됩니다.
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	baseDirFollowsSourceFile()
	dependsOn test
}

tasks.register('copyDocument', Copy) {
	dependsOn asciidoctor

	from file("build/docs/asciidoc/")
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

